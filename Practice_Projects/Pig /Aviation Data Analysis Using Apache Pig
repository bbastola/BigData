Aviation Data Analysis Using Apache Pig

The U.S. Department of Transportation’s (DOT) Bureau of Transportation Statistics (BTS) tracks the on-time performance of domestic flights operated by large air carriers. Summary information on the number of on-time, delayed, canceled, and diverted flights appears in DOT’s monthly Air Travel Consumer Report, published about 30 days after the month’s end, as well as in summary tables posted on this website. Summary statistics and raw data are made available to the public at the time the Air Travel Consumer Report is released.

You can download the datasets from the following links:

Delayed_Flights.csv - https://drive.google.com/file/d/0B_Qjau8wv1KoWTVDUVFOdzlJNWM/view
Airports.csv - https://drive.google.com/file/d/0B_Qjau8wv1KocDR3djk1Qm96Mmc/view

These are 2 different datasets, i.e., Delayed_Flights.csv and Airports.csv. Let us understand one at a time.

Delayed_Flights.csv Datasets

There are 29 columns in this dataset. Some of them have been mentioned below:

Year: 1987 – 2008
Month: 1 – 12
FlightNum: Flight number
Canceled: Was the flight canceled?
CancelleationCode: The reason for cancellation.
For complete details, refer to this link https://drive.google.com/file/d/0B_Qjau8wv1KoTFljWHlxMDE0Szg/view

Airports.csv Datasets

iata: the international airport abbreviation code
name of the airport
city and country in which airport is located.
lat and long: the latitude and longitude of the airport

Problem Statement 1: Find out the top 5 most visited destinations.

	delayedFlights = Load '/user/bbastola/Practice/DelayedFlights.csv' USING org.apache.pig.piggybank.storage.CSVExcelStorage(',','NO_MULTILINE','UNIX','SKIP_INPUT_HEADER');

	filteredData = FOREACH delayedFlights GENERATE (int)$1 as Year, (int)$10 as flightNum, (chararray)$17 as Origin, (chararray)$18 as Destination;
	limitData = LIMIT filteredData 10;

	filteredDataNotNull = FILTER filteredData by Destination is not null;

	destinationGroupBy = GROUP filteredDataNotNull BY Destination;

	groupedData = FOREACH destinationGroupBy GENERATE group, COUNT(filteredDataNotNull.Destination)

	orderedDestination = ORDER groupedData by $1 Desc;

	topFiveDestination = LIMIT orderedDestination 5;

Now we have to join this relation with airpor relation to get the actual names

	Airport = Load '/user/bbastola/Practice/airports.csv' USING org.apache.pig.piggybank.storage.CSVExcelStorage(',','NO_MULTILINE','UNIX','SKIP_INPUT_HEADER');

	filteredAirport = FOREACH Airport GENERATE (chararray)$0 as IATA, (chararray)$2 as City, (chararray)$4 as Country;

	dataJoin = JOIN topFiveDestination BY $0, filteredAirport BY $0;

	dump dataJoin;

Problem Statement 2: Which month has seen the most number of cancellations due to bad weather?

delayedFlights = Load '/user/bbastola/Practice/DelayedFlights.csv' USING org.apache.pig.piggybank.storage.CSVExcelStorage(',','NO_MULTILINE','UNIX','SKIP_INPUT_HEADER');

extractedData = FOREACH delayedFlights GENERATE (int)$2 as Month, (chararray)$10 as FlightNum, (int)$22 as Cancelled, (chararray)$23 as cancellationCode;

extractedDataNotNull = FILTER extractedData by Cancelled is not null;

filteredData = FILTER extractedDataNotNull BY (Cancelled == 1 AND cancellationCode == 'B');

groupByMonth = GROUP filteredData BY $0;

groupedData = FOREACH groupByMonth GENERATE group, COUNT(filteredData.Cancelled);

orderedData = ORDER groupedData BY $1 desc;

FinalResult = LIMIT orderedData 1;

dump FinalResult;

















